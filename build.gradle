import groovy.json.JsonOutput
import groovy.json.JsonSlurper

/**
 * Get all the Neo4j releases
 * The source of truth is https://api.github.com/repos/neo4j/neo4j/releases
 * We don't consider all the tags with "prerelease" attribute to true
 *
 * @return
 */
def collectNeo4jReleases() {
    def slurper = new JsonSlurper()

    // get from tags or releases?
    // https://api.github.com/repos/neo4j/neo4j/releases
    def releases = slurper.parseText(new URL("http://search.maven.org/solrsearch/select?q=g:%22org.neo4j%22+AND+a:%22neo4j%22&core=gav&rows=500&wt=json").getText())
    releases = releases.response.docs

    def actualReleases = []
    for (release in releases) {
        actualReleases.add(release.v)
    }

    // We sort considering that X.Y.Z-SOMETHING should be considered lower than X.Y.Z --> it's not a lexicographical ordering
    return actualReleases
            .findAll { it >= "3.0" }
            .sort{ o1, o2 -> orderVersion(o1) <=> orderVersion(o2) }
            .reverse()
}

def orderVersion(str) {
  return str.split("[.-]").collect{ it =~ "^\\d+\$" ? it.padLeft(4,"0") : it.replace("beta","mbeta").replace("BETA","MBETA") }.join(".") + "/"
}
/**
 * Coupling Neo4j with Neo4j GraphQL
 *
 * There are two heuristic in place:
 *
 * - latest version of Neo4j with latest version of Neo4j-GraphQL
 * - if there is no available version of Neo4j-GraphQL for a given NEO4j release than take the prior available version of Neo4j-GraphQL
 */
task versions {
    def neo4jReleases = collectNeo4jReleases()

    def slurper = new JsonSlurper()

    // get tags
    def pattern = ~/<neo4j.version>(.+?)<\/neo4j.version>/
    def releases = slurper.parseText(new URL("https://api.github.com/repos/neo4j-graphql/neo4j-graphql/releases?page=1&per_page=500").getText())
    def versions = []
    def neo4jToGraphQL = [:]
    def latestGraphQL, oldestGraphQL
    for (release in releases) {
        if (release.draft) continue;
        def buildText = new URL("https://raw.githubusercontent.com/neo4j-graphql/neo4j-graphql/" + release.tag_name + "/pom.xml").getText()
        def matcher = pattern.matcher(buildText)
        for (def i = 0; i < matcher.getCount(); i++) {
            def neo4jVersion = matcher[i][1]

            println "neo " + neo4jVersion + ", graphQl " + release.tag_name 
            if (!neo4jToGraphQL.containsKey(neo4jVersion)) {
                neo4jToGraphQL[neo4jVersion] = release.tag_name
            }

            if (latestGraphQL == null) {
                latestGraphQL = release.tag_name
            }

            oldestGraphQL = release.tag_name
        }
    }

    println "LATEST ALGOS IS " + latestGraphQL + " WHILE OLDEST IS " + oldestGraphQL
    println(neo4jToGraphQL)
    println(neo4jReleases)
    for (neo4jRelease in neo4jReleases) {
        def prefix = neo4jRelease.substring(0,Math.min(4,neo4jRelease.length()))
        if (prefix < "3.0") continue;

        def graphQlVersion = neo4jToGraphQL[neo4jRelease]

        // If there is no GraphQL release for a Neo4j release we have to guess which one to associate
        if (graphQlVersion == null) {
            def matchingNeo4jVersions = neo4jToGraphQL.keySet().findAll{ it.substring(0,4) == prefix }.sort{ o1, o2 -> orderVersion(o1) <=> orderVersion(o2) }
            def matchingGraphQLVersions = neo4jToGraphQL.subMap(matchingNeo4jVersions).values().sort{ o1, o2 -> orderVersion(o1) <=> orderVersion(o2) }
            matchingNeo4jVersions = matchingNeo4jVersions.findAll{ orderVersion(it) < orderVersion(neo4jRelease) }
            def matchingNeo4jVersion = matchingNeo4jVersions.isEmpty() ? null : matchingNeo4jVersions[-1]
            def firstGraphQLVersion = matchingGraphQLVersions.isEmpty() ? null : matchingGraphQLVersions[0]
            graphQlVersion = neo4jToGraphQL[matchingNeo4jVersion]==null ? firstGraphQLVersion : neo4jToGraphQL[matchingNeo4jVersion]
        }
        if (graphQlVersion != null) {
            def version = [ neo4j : neo4jRelease, version : graphQlVersion, 
                            url : "http://github.com/neo4j-graphql/neo4j-graphql/releases/" + graphQlVersion,
                            jar: "https://github.com/neo4j-graphql/neo4j-graphql/releases/download/${graphQlVersion}/neo4j-graphql-${graphQlVersion}.jar" ]
            versions.add(version)
        }
    }

    def versionsFile = new File("versions.json")

    versionsFile.write(JsonOutput.prettyPrint(JsonOutput.toJson(versions)))
}
